import com.github.spotbugs.snom.SpotBugsTask

plugins {
    id 'java-library'
    id "com.github.spotbugs" version "4.7.0"
    id 'checkstyle'
    id 'maven-publish'
//    id 'maven'
    id 'signing'
}

group = 'io.github.thecodinglog'
version = '0.2.6'
sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

repositories {
    jcenter()
    mavenLocal()
    mavenCentral()
}

checkstyle {
    configFile = file("${project.rootDir}/config/checkstyle/checkStyle.xml")
    configProperties = ["suppressionFile": "${project.rootDir}/config/checkstyle/checkStyle-suppressions.xml"]
    toolVersion = "8.40"
    ignoreFailures = false
    maxErrors = 0
    maxWarnings = 0
}

spotbugs {
    ignoreFailures = false
    reportLevel = "high"
    spotbugsTest.enabled = false
}
tasks.withType(SpotBugsTask) {
    reports {
        text.enabled = true
        xml.enabled = false
        html.enabled = false
    }
}

tasks.register("printSpotbugsMain") {
    doLast {
        File mainResult = file("${buildDir}/reports/spotbugs/main.txt")
        if (mainResult.exists()) {
            mainResult.readLines().forEach {
                println(it)
            }
        }
    }
}

tasks.getByPath("spotbugsMain").finalizedBy("printSpotbugsMain")

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}
test {
    useJUnitPlatform()
}
java {
    withJavadocJar()
    withSourcesJar()
}

dependencies {
    implementation group: 'org.slf4j', name: 'slf4j-api', version: '1.7.36'
    implementation('org.springframework:spring-core:5.3.20')
    implementation("com.fasterxml.jackson.core:jackson-databind:2.13.3")
    implementation 'com.google.code.findbugs:jsr305:3.0.2'
    testImplementation 'com.github.javafaker:javafaker:1.0.2'
    testImplementation('org.assertj:assertj-core:3.22.0')
    testImplementation('org.mockito:mockito-all:1.10.19')
    testImplementation('org.junit.jupiter:junit-jupiter-api:5.8.2')
    testRuntimeOnly('org.junit.jupiter:junit-jupiter-engine')
}

publishing {
    repositories {
        maven {
            name "OSSRH"
            url "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                username System.getenv("MAVEN_USERNAME")
                password System.getenv("MAVEN_PASSWORD")
            }
        }
    }

    publications {
        maven(MavenPublication) {
            artifactId 'method-invoker'
            from components.java

            pom {
                name = "method-invoker"
                description = "Method-Invoker is a tool for invoking methods at runtime using class names and method modifiers."
                url = "https://github.com/thecodinglog/method-invoker"

                licenses {
                    license {
                        name = "MIT License"
                        url = "https://github.com/thecodinglog/method-invoker/blob/master/LICENSE"
                    }
                }
                developers {
                    developer {
                        id = "thecodinglog"
                        name = "Jeongjin Kim"
                        email = "thefuturecreator@gmail.com"
                    }
                }
                scm {
                    connection = "https://github.com/thecodinglog/method-invoker.git"
                    developerConnection = "https://github.com/thecodinglog/method-invoker.git"
                    url = "https://github.com/thecodinglog/method-invoker"
                }
            }
        }
    }
}
//signing {
//    def signingKey = System.getenv("SIGNING_KEY")
//    def signingPassword = System.getenv("SIGNING_PASSWORD")
//    useInMemoryPgpKeys(signingKey, signingPassword)
//    sign publishing.publications.maven
//}